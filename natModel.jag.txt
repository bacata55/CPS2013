model {
      ## Model DGP
      for (i in 1:n.obs){ 
      	  for (a in 1:3) {
	      eta[i,a] <- X[i,1:n.beta] %*% beta[1:n.beta,a] #And you wonder why I use JAGS instead of BUGS...
	      expeta[i,a] <- exp(eta[i,a])
	      prob[i,a] <- expeta[i,a]/sum(expeta[i,1:3])
	      }
	  y[i,] ~ dmulti(prob[i,],n[i])
      }

      ## Prior distributions
      for (i in 1:n.beta){
      	  beta[i,1] <- 0   # identifying assumption
      }
      beta[1:n.beta,2] ~ dmnorm(mu.beta2,sigma.beta)
      beta[1:n.beta,3] ~ dmnorm(mu.beta3,sigma.beta)	
      
      ## Marginal Effect of Similarity/Homogeneity
      for (i in 1:100){
      	  for(a in 1:3){
      	  	mareffSim[i,a]  <- beta[3,a] + sim.nat[i]*beta[4,a]
      	  }	
      }
	
}